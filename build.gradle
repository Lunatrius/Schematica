buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.0-SNAPSHOT'
    }
}

apply plugin: "forge"

ext.configFile = file "build.properties"

configFile.withReader {
    def prop = new Properties()
    prop.load(it)
    ext.config = new ConfigSlurper().parse prop
}

ext.build = System.getenv().BUILD_NUMBER ?: "dev"

group = "com.github.lunatrius.${config.mod.package}"
version = "${config.version.major}.${config.version.minor}.${config.version.micro}.${build}"
archivesBaseName = "[${config.version.minecraft}]${config.mod.id}"

minecraft {
    version = "${config.version.minecraft}-${config.version.forge}"
    assetDir = "run/assets"
}

processResources {
    from (sourceSets.main.resources.srcDirs) {
        include "mcmod.info"
        include "version.properties"

        expand (["modid": config.mod.id,
            "version": version,
            "mcversion": config.version.minecraft,
            "forgeversion": config.version.forge
        ])
    }

    from (sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
        exclude "version.properties"
    }
}

if (!project.hasProperty("keystore_location"))
    ext.keystore_location = "."

if (!project.hasProperty("keystore_alias"))
    ext.keystore_alias = ""

if (!project.hasProperty("keystore_password"))
    ext.keystore_password = ""

if (!project.hasProperty("release_location"))
    ext.release_location = "."
else
    ext.release_location = release_location.replace("{modid}", config.mod.id).replace("{mcversion}", config.version.minecraft).replace("{version}", version)

task signJar(dependsOn: "reobf") {
    inputs.file jar.getArchivePath()
    inputs.file keystore_location
    inputs.property "keystore_alias", keystore_alias
    inputs.property "keystore_password", keystore_password
    outputs.file jar.getArchivePath()

    onlyIf {
        return keystore_location != "."
    }

    doLast {
        ant.signjar(
            destDir: jar.destinationDir,
            jar: jar.getArchivePath(),
            keystore: keystore_location,
            alias: keystore_alias,
            storepass: keystore_password
        )
    }
}

task release(dependsOn: "signJar", type: Copy) {
    from project.tasks.jar.destinationDir
    into project.file(project.release_location)

    eachFile { file ->
        logger.info "copying ${file}"
    }

    onlyIf {
        return project.release_location != "."
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'src'
}

// temporary :deobf fix
// reobf {
//     deobfFile = new net.minecraftforge.gradle.delayed.DelayedFile(project, "build/tmp/deobfBinJar/deobfed.jar")
// }
